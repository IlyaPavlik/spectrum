apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 26
        versionCode 3
        versionName "0.3"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // there will be a lot of useless lint warnings if delete this line
        disable 'AllowBackup'
        // for most projects accessibility tools support is out of scope
        disable 'ContentDescription'
        // a lot of Java libraries have references to Java SE packages
        disable 'InvalidPackage'
        // sp can be scaled by the user's font size preference so can break design
        disable 'SpUsage'
        // there are no pixel art for the most projects
        disable 'IconMissingDensityFolder'
        // it is just reminder
        disable 'SelectableText'
        // there are too few projects with RTL support
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.11.0"
    testImplementation "org.powermock:powermock-module-junit4:1.7.3"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.7.3"
    testImplementation "org.powermock:powermock-api-mockito:1.6.4"
    testImplementation "org.powermock:powermock-classloading-xstream:1.7.3"

    //Google Support
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:palette-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    //Google Firebase
    implementation 'com.google.firebase:firebase-core:16.0.1'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compileOnly 'org.projectlombok:lombok:1.18.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.0'

    testCompileOnly 'org.projectlombok:lombok:1.18.0'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.0'

    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    implementation 'com.squareup:otto:1.3.8'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.1.9'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    implementation 'com.github.bumptech.glide:glide:4.4.0'
    implementation 'com.google.code.gson:gson:2.8.2'
}

apply plugin: 'com.google.gms.google-services'
