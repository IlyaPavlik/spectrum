apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 3
        versionName "0.3"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // there will be a lot of useless lint warnings if delete this line
        disable 'AllowBackup'
        // for most projects accessibility tools support is out of scope
        disable 'ContentDescription'
        // a lot of Java libraries have references to Java SE packages
        disable 'InvalidPackage'
        // sp can be scaled by the user's font size preference so can break design
        disable 'SpUsage'
        // there are no pixel art for the most projects
        disable 'IconMissingDensityFolder'
        // it is just reminder
        disable 'SelectableText'
        // there are too few projects with RTL support
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "org.powermock:powermock-module-junit4:1.6.4"
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.4"
    testCompile "org.powermock:powermock-api-mockito:1.6.4"
    testCompile "org.powermock:powermock-classloading-xstream:1.6.4"

    //Google Support
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:palette-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    //Google Firebase
    compile 'com.google.firebase:firebase-core:11.0.2'
    compile 'com.google.firebase:firebase-crash:11.0.2'

    compile 'com.jakewharton:butterknife:8.7.0'
    apt 'com.jakewharton:butterknife-compiler:8.7.0'

    provided 'org.projectlombok:lombok:1.16.18'
    apt "org.projectlombok:lombok:1.16.18"

    compile 'com.google.dagger:dagger:2.6.1'
    apt 'com.google.dagger:dagger-compiler:2.6.1'

    compile 'com.squareup:otto:1.3.8'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.9'

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.tony19:logback-android-core:1.1.1-6'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.code.gson:gson:2.8.0'
}

apply plugin: 'com.google.gms.google-services'
