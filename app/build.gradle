apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion config.sdk.compile

    defaultConfig {
        minSdkVersion config.sdk.min
        targetSdkVersion config.sdk.target
        versionCode config.build.code
        versionName config.build.name
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // there will be a lot of useless lint warnings if delete this line
        disable 'AllowBackup'
        // for most projects accessibility tools support is out of scope
        disable 'ContentDescription'
        // a lot of Java libraries have references to Java SE packages
        disable 'InvalidPackage'
        // sp can be scaled by the user's font size preference so can break design
        disable 'SpUsage'
        // there are no pixel art for the most projects
        disable 'IconMissingDensityFolder'
        // it is just reminder
        disable 'SelectableText'
        // there are too few projects with RTL support
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Kotlin
    implementation library.kotlin

    //Tests
    testImplementation library.junit
    testImplementation library.mockito
    testImplementation library.powermockJunit
    testImplementation library.powermockJunitRule
    testImplementation library.powermockMockito
    testImplementation library.powermockClassloading

    //Google Support
    implementation library.recyclerView
    implementation library.appcompat
    implementation library.palette
    implementation library.cardView
    implementation library.multidex
    implementation library.material

    //Google Firebase
    implementation library.firebaseCore
    implementation library.firebaseAnalytics
    implementation library.firebaseCrashlytics

    //Dependency injection
    implementation library.dagger
    kapt library.daggerCompiler

    implementation library.otto

    //Rx
    implementation library.rxjava
    implementation library.rxandroid

    //Logger
    implementation library.slf4j
    implementation library.logbackCore
    implementation(library.logbackClassic) {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    implementation library.glide
    kapt library.glideCompiler
    implementation library.gson

    //Moxy
    implementation library.moxy
    kapt library.moxyCompiler
    implementation library.moxyAndroid
    implementation library.moxyAndroidX
    implementation library.moxyMaterial

    //Cicerone
    implementation library.cicerone

    //Room
    implementation library.room
    kapt library.roomCompiler
    implementation library.roomRxJava

    /* Tools */
    implementation library.viewBindingDelegate
}

apply plugin: 'com.google.gms.google-services'
