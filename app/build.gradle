apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 8
        versionName "0.6.2"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // there will be a lot of useless lint warnings if delete this line
        disable 'AllowBackup'
        // for most projects accessibility tools support is out of scope
        disable 'ContentDescription'
        // a lot of Java libraries have references to Java SE packages
        disable 'InvalidPackage'
        // sp can be scaled by the user's font size preference so can break design
        disable 'SpUsage'
        // there are no pixel art for the most projects
        disable 'IconMissingDensityFolder'
        // it is just reminder
        disable 'SelectableText'
        // there are too few projects with RTL support
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.32"

    //Tests
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-core:3.2.4"
    testImplementation "org.powermock:powermock-module-junit4:1.7.4"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.7.4"
    testImplementation "org.powermock:powermock-api-mockito2:1.7.4"
    testImplementation "org.powermock:powermock-classloading-xstream:1.7.4"

    //Google Support
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.4.0'

    //Google Firebase
    implementation 'com.google.firebase:firebase-core:19.0.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.0'

    implementation 'com.jakewharton:butterknife:10.2.0'
    kapt 'com.jakewharton:butterknife-compiler:10.2.0'

    //Dependency injection
    implementation 'com.google.dagger:dagger:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.28.3'

    implementation 'com.squareup:otto:1.3.8'

    //Rx
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.8'

    //Logger
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    //Moxy
    implementation "com.github.moxy-community:moxy:2.2.2"
    kapt "com.github.moxy-community:moxy-compiler:2.2.2"
    implementation "com.github.moxy-community:moxy-android:2.2.2"
    implementation "com.github.moxy-community:moxy-androidx:2.2.2"
    implementation "com.github.moxy-community:moxy-material:2.2.2"

    //Cicerone
    implementation 'com.github.terrakok:cicerone:7.1'

    //Room
    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.room:room-rxjava2:2.3.0'
}

apply plugin: 'com.google.gms.google-services'
