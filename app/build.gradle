plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlin-kapt")
    id("com.google.firebase.crashlytics")
    id("dagger.hilt.android.plugin")
    id("androidx.navigation.safeargs")
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion config.sdk.compile

    defaultConfig {
        minSdkVersion config.sdk.min
        targetSdkVersion config.sdk.target
        versionCode config.build.code
        versionName config.build.name
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // there will be a lot of useless lint warnings if delete this line
        disable 'AllowBackup'
        // for most projects accessibility tools support is out of scope
        disable 'ContentDescription'
        // a lot of Java libraries have references to Java SE packages
        disable 'InvalidPackage'
        // sp can be scaled by the user's font size preference so can break design
        disable 'SpUsage'
        // there are no pixel art for the most projects
        disable 'IconMissingDensityFolder'
        // it is just reminder
        disable 'SelectableText'
        // there are too few projects with RTL support
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    /* Kotlin */
    implementation library.kotlin
    implementation library.coroutines

    //Tests
    testImplementation library.junit
    testImplementation library.mockito
    testImplementation library.powermockJunit
    testImplementation library.powermockJunitRule
    testImplementation library.powermockMockito
    testImplementation library.powermockClassloading
    testImplementation library.coroutinesTest
    testImplementation library.kotlinTest

    //Google Support
    implementation library.recyclerView
    implementation library.appcompat
    implementation library.palette
    implementation library.cardView
    implementation library.multidex
    implementation library.material
    implementation library.activity

    //Google Firebase
    implementation library.firebaseCore
    implementation library.firebaseAnalytics
    implementation library.firebaseCrashlytics

    implementation library.glide
    kapt library.glideCompiler
    implementation library.gson

    //Moxy
    implementation library.moxy
    kapt library.moxyCompiler
    implementation library.moxyAndroid
    implementation library.moxyAndroidX
    implementation library.moxyMaterial

    //Navigation
    implementation library.navigation
    implementation library.navigationUi

    //Room
    implementation library.room
    kapt library.roomCompiler
    implementation library.roomKtx

    /* Dependency injection */
    implementation library.hilt
    kapt library.hiltCompiler

    /* Tools */
    implementation library.camera
    implementation library.cameraLifecycle
    implementation library.cameraView
    implementation library.viewBindingDelegate

    /* Logger */
    implementation library.slf4j
    implementation library.logback
}

apply plugin: 'com.google.gms.google-services'
